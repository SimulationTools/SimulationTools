#!/usr/bin/env RunMathematicaScript
(* -*- mode: mathematica -*- *)

$Path = Prepend[$Path, "/home/ianhin/Cactus/ETBG/repos"];

If[FindFile["SimulationTools`"] === $Failed,
  Print["Cannot find SimulationTools.  Please make sure that it is accessible on your $Path."];
  Quit[1]];

Needs["CCompilerDriver`"];
$CCompiler = {"Compiler" -> CCompilerDriver`IntelCompiler`IntelCompiler};

Get["SimulationTools`"];

SetOptions[$Output, FormatType -> OutputForm];
AbortOnMessagesST[True];

CheckAbort[

(* For testing *)
(* Print["Eccentricity=", 0.01]; *)
(* Print["NextD=", 10.1]; *)
(* Print["NextPr=", -0.004]; *)
(* Quit[0]; *)

sim = $ScriptCommandLine[[2]];
outfile = $ScriptCommandLine[[3]];

If[SimulationNames[sim] === {},
  Error["Cannot find simulation "<>sim]];
  $SimulationPath = {FileNameDrop[sim,-1]};
  
  (* Find all the simulations in the eccentricity reduction series *)
  sims =  Replace[StringCases[sim, (a__~~"_e"~~(it:NumberString)~~"_"~~b__) :> {a,it,b}],
    { {} :> (Print["Simulation name ", sim, " is not of the form *_e<int>_*"]; Quit[1]),
      {{a_,it_,b_}} :> (pattern = FileNameTake[a,-1]~~"_e"~~NumberString~~"_"~~b;
        Print["pattern = ", pattern];
        (* For some reason, SimulationNames doesn't work with full paths containing patterns *)
        SimulationNames[pattern]),
      a_ :> Print["Unrecognised match: ", a]}];

  Print["sims = ", sims];

  simName = FileNameTake[sim,-1];
  nSims = LengthWhile[sims, (# =!= simName) &] + 1;
  If[nSims > Length[sims],
    Print["Cannot find current simulation ", FileNameTake[sim,-1],
      " in the list of simulations ", sims];
    Quit[1]];

  sims = Take[sims, nSims];

  Print["Analysing ", sims];
  Print["$SimulationPath = ", $SimulationPath];
  
  Print["PunctureTracker files: ", FindSimulationFiles[sims[[-1]], 
    "puncturetracker" ~~ ("-" | "::") ~~ "pt_loc..asc"]];
  
  (* Analyse all the simulations, to get information from the previous
     ones to help at low eccentricity when the fit is difficult *)

  (* NB: cannot use simulation basename as there is a directory of
     that name in the current directory which is found
     preferentially. *)
  eccs = SimulationEccentricityAnalysis[FileNameJoin[{$SimulationPath[[1]],#}] & /@ sims];

  Print[CForm/@eccs[[All, {"Eccentricity", "ResidualNorm", "RadialPeriod", "OrbitalPeriod"}]]];
  ecc = Last[eccs];
  
  f = OpenWrite[outfile];

  If[ecc["Failed"] === False,
    WriteLine[f, "Eccentricity="<>ToString[ecc["Eccentricity"], CForm]];
    {nextD, nextPr} = ReduceEccentricity[sim, ecc];
    WriteLine[f, "NextD="<>ToString[nextD,CForm]];
    WriteLine[f, "NextPr="<>ToString[nextPr,CForm]],
    (* else *)
    Print["Failed to determine eccentricity"];
    Quit[1]
    ];
  Close[f];
  , Quit[1]];
